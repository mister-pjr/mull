10 ! MULTI USER LUNAR LANDER (C)2024 - CLIENT 

100 %INCLUDE "COMDEF"

150	DECLARE BYTE PLAYER, DUM, OLDX(1), OLDY(1), I, J, NCMD, & 
	OLDMISL(1,1), OLDMISR(1,1)
	DECLARE STRING STRPLA(1), GRDLIN(3)
	DECLARE BYTE FUNCTION UPDPLA
	DECLARE BYTE FUNCTION GETPLA
	DECLARE BYTE FUNCTION SHIP(BYTE)
	DECLARE BYTE FUNCTION PXY(BYTE, BYTE, STRING)
	DECLARE BYTE FUNCTION SETXY(BYTE,BYTE)
	DECLARE BYTE FUNCTION CTE(BYTE, BYTE)

500 ON ERROR GOTO ERRHDL
	DUM=CTRLC

1000 GOSUB CLR
	PRINT "MULTI USER LUNAR LANDER - CLIENT"
	PRINT ""
	PRINT "WAITING FOR ANOTHER PLAYER..."
1100 PLAYER = GETPLA
	IF PLAYER = 99 THEN 
		PRINT "TOUGH LUCK! NO MORE SEATS"
		PRINT "BYE"
		STOP
	END IF
1150 STRPLA(PLAYER)=CHR$(PLAYER+49)
	IF PLAYER=0 THEN
		STRPLA(1)=CHR$(50)
	ELSE
		STRPLA(0)=CHR$(49)
	END IF

1200 STRTROU:
	WHILE (ACT(PLAYER)<>1) \ NEXT ! WAITING FOR THE SIGNAL TO DRAW
	GOSUB SCRGAME \ GOSUB TERRAIN
	ACT(PLAYER)=2 ! WE FINISHED DRAWING
1250 	WHILE ACT(PLAYER)>0 \ NEXT	! WAITING FOR THE SIGNAL TO START
1260 	DUM=PXY(1,1,"         *")

1300 MLOOP: ! LOOP  
 ! UPD SCR STATS
	GOSUB UPDSCR

 ! SHIPS
	IF H(0)>=0 THEN DUM=SHIP(0) END IF
	IF H(1)>=0 THEN DUM=SHIP(1) END IF

 ! MISSILES
	GOSUB MISUPD

 ! UPDATE STATUS IF NEEDED 	
	IF ACT(PLAYER)<>0 THEN GOSUB ENDRUND END IF

 ! GET INPUT
	DUM=PXY(1,16," ")
	DUM=SETXY(1,15)
 	WAIT 1
      	NCMD=ONECHR(0%)
	GET #0%
 	MOVE FROM #0%, COMM(PLAYER)

1350 IF ACT(0)=0 OR ACT(1)=0 THEN GOTO MLOOP END IF
 ! UPDATE STATUS IF VICTORY
	IF ACT(PLAYER)=5 THEN
		GOSUB ENDRUND
	END IF
	SLEEP 2

        ACT(PLAYER)=10 ! PLAYER ACK THAT ROUND HAS FINISHED
        WHILE ACT(PLAYER)=10 \ NEXT ! WAIT FOR REPLY

1360 IF ACT(PLAYER)=1 THEN GOTO STRTROU END IF
	GOSUB CLR
	EXIT PROGRAM
	
1500	ENDRUND:
	GOSUB INVERT
 	IF ACT(PLAYER)=9 THEN
		DUM=PXY(15,12,"****** YOU CRASHED INTO THE OTHER SHIP ******")
		PRINT STR$(7)
	END IF
	IF ACT(PLAYER)=8 THEN
		DUM=PXY(15,12,"****** YOU LANDED OUTSIDE THE PAD ******")
		PRINT STR$(7)
	END IF
	IF ACT(PLAYER)=7 THEN
		DUM=PXY(15,12,"****** YOU CRASHED ******")
		PRINT STR$(7)
	END IF
	IF ACT(PLAYER)=6 THEN
		DUM=PXY(15,12,"****** YOU WERE HIT BY A MISSILE ******")
		BLOWN(PLAYER)=1
		PRINT STR$(7)
	END IF
	IF ACT(PLAYER)=5 THEN
		DUM=PXY(15,12,"****** YOU SURVIVED !!!!! ******")
		PRINT STR$(7)+STR$(7)
	END IF
	GOSUB NORMAL
	DUM=SETXY(1,15)
	RETURN

3600 DEF BYTE GETPLA ! GET PLAYER NUMBER
	IF CHKIN(0)=0 THEN 
		CHKIN(0)=1
		GETPLA=0
	ELSE
		IF CHKIN(1)=0 THEN
			CHKIN(1)=1
			GETPLA=1
		ELSE 
			GETPLA=99
		END IF
	END IF	
	END DEF

3700 SCRGAME:
	GOSUB CLR
	FOR I=1 TO 24
		DUM=PXY(10,I,"*")
	NEXT I
	GOSUB INVERT
	DUM=PXY(1,1,"NOTSTARTED")
	GOSUB NORMAL
	DUM=PXY(1,2,"***MULL***")
	DUM=PXY(1,4,"PLAYER "+STRPLA(PLAYER))	
	DUM=PXY(1,10,"*********")
	DUM=PXY(1,12,"COMMANDS")
	DUM=PXY(1,13,"*********")
	DUM=PXY(1,14," C-LEFT")
	DUM=PXY(1,15," V-RIGHT")
	DUM=PXY(1,16," D-MIS.L")
	DUM=PXY(1,17," F-MIS.R")
	DUM=PXY(1,18," PWR:0-9")
	DUM=PXY(1,19,"*********")
	GOSUB INVERT
	DUM=PXY(1,20,"*SCORE*")
	DUM=PXY(1,21,"P1   P2")
	GOSUB NORMAL
	DUM=SETXY(1,22)
	PRINT SCORE(0);"  ";SCORE(1)
	DUM=SETXY(1,23)
	PRINT "RNDS:";ROUNDS
	RETURN

3800 TERRAIN:
	GRDLIN(0)="" \ GRDLIN(1)="" \ GRDLIN(2)="" \ GRDLIN(3)=""
	FOR I=0 TO 68
		IF TERRA(I)>=100 THEN
			FOR J=0 TO 2 
				GRDLIN(J)=GRDLIN(J)+" "
			NEXT J
			GRDLIN(3)=GRDLIN(3)+"_"
		ELSE
			FOR J=0 TO 3
				IF J>=(TERRA(I)-21) THEN
					GRDLIN(J)=GRDLIN(J)+"O"
				ELSE
					GRDLIN(J)=GRDLIN(J)+" "
				END IF
			NEXT J
		END IF	
	NEXT I
	FOR I=0 TO 3 \ DUM=PXY(11,21+I,GRDLIN(I)) \ NEXT I
	RETURN

3900 DEF BYTE SHIP (BYTE PLA) ! DRAW SHIP
 ! CLEAR OLD SHIP
 	IF OLDY(0)<>0 OR OLDY(1)<>0 THEN
		DUM=PXY(OLDX(PLA),OLDY(PLA)," ")
		DUM=PXY(OLDX(PLA),OLDY(PLA)-1," ")
		DUM=PXY(OLDX(PLA)-1,OLDY(PLA)+1," ")
		DUM=PXY(OLDX(PLA)+1,OLDY(PLA)+1," ")
 	END IF
 ! DRAW NEW SHIP
3950	OLDX(PLA)=LOCX(PLA)
	OLDY(PLA)=LOCY(PLA)
	IF BLOWN(PLA)=1 THEN
		DUM=PXY(LOCX(PLA),LOCY(PLA),"*")
	ELSE
		DUM=PXY(LOCX(PLA),LOCY(PLA),STRPLA(PLA))
		DUM=PXY(LOCX(PLA),LOCY(PLA)-1,"A")
		DUM=PXY(LOCX(PLA)-1,LOCY(PLA)+1,"/")
		DUM=PXY(LOCX(PLA)+1,LOCY(PLA)+1,"\")
	END IF
	END DEF

4000 UPDSCR:
	DUM=PXY(1,5,"H: "+STR$(H(PLAYER))+" ")
	DUM=PXY(1,6,"S: "+STR$(S(PLAYER))+" ")
	DUM=PXY(1,7,"F: "+STR$(F(PLAYER))+" ")
	DUM=PXY(1,8,"P: "+STR$(P(PLAYER))+" ")
	IF SIDE(PLAYER)<0 THEN 
		DUM=PXY(1,9,"SID: L"+STR$(ABS%(SIDE(PLAYER))))
	END IF
	IF SIDE(PLAYER)>0 THEN
 		DUM=PXY(1,9,"SID: R"+STR$(ABS%(SIDE(PLAYER))))
	END IF
	IF SIDE(PLAYER)=0 THEN
		DUM=PXY(1,9,"        ")	
	END IF
	RETURN

5000 MISUPD:
	IF OLDMISL(0,0)<>0 THEN
                DUM=PXY(OLDMISL(0,0),OLDMISL(0,1)," ")
	END IF
	IF MISL(0,0)<>0 THEN
		OLDMISL(0,0)=MISL(0,0)
		OLDMISL(0,1)=MISL(0,1)
		DUM=PXY(MISL(0,0),MISL(0,1),"=")
	END IF
        IF OLDMISL(1,0)<>0 THEN
                DUM=PXY(OLDMISL(1,0),OLDMISL(1,1)," ")
	END IF
        IF MISL(1,0)<>0 THEN
		OLDMISL(1,0)=MISL(1,0)
		OLDMISL(1,1)=MISL(1,1)
		DUM=PXY(MISL(1,0),MISL(1,1),"=")
  	END IF
        IF OLDMISR(0,0)<>0 THEN
                DUM=PXY(OLDMISR(0,0),OLDMISR(0,1)," ")
 	END IF
        IF MISR(0,0)<>0 THEN
		OLDMISR(0,0)=MISR(0,0)
		OLDMISR(0,1)=MISR(0,1)
		DUM=PXY(MISR(0,0),MISR(0,1),"=")
	END IF
        IF OLDMISR(1,0)<>0 THEN
                DUM=PXY(OLDMISR(1,0),OLDMISR(1,1)," ")
	END IF
	IF MISR(1,0)<>0 THEN
		OLDMISR(1,0)=MISR(1,0)
		OLDMISR(1,1)=MISR(1,1)
		DUM=PXY(MISR(1,0),MISR(1,1),"=")
	END IF 
	RETURN

6000 ERRHDL:
  	IF ERR=15 THEN RESUME 1350 ELSE RESUME END IF

7000 %INCLUDE "COMCODE"








