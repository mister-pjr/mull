10 ! MULTI USER LUNAR LANDER (C)2024 

100 %INCLUDE "COMDEF"

110 DECLARE BYTE LZSTART, LZEND, DUM, OTHER, MAXSPD
	DECLARE WORD H0, S0, F0, I, OLDACL(1), TGTACL(1), CURACL(1), &
	T(1), S1, TMP
	DECLARE BYTE CONSTANT MINDIST=5, MINSEP=3, SIDSIZ=8,&
	MTNHEI=4, SCRHEI=24, STRTX=11, G=5, SIDEG=1
	DECLARE SINGLE HADJ, H0F, HF, H24
	DECLARE STRING YESNO, A$, TMPCHR
	DECLARE BYTE FUNCTION PXY(BYTE, BYTE, STRING)
	DECLARE BYTE FUNCTION CTE(BYTE, BYTE)
	DECLARE BYTE FUNCTION PLAUPD(BYTE)
	DECLARE BYTE FUNCTION DETKIL(BYTE)

150 H0=2400 \ S0=-30 \ F0=500 \ MAXSPD=-15 \ ACT(0)=9 \ ACT(1)=9
	CHKIN(0)=0 \ CHKIN(1)=0 \ SCORE(0)=0 \ SCORE(1)=0 \ ROUNDS=0

200 GOSUB CLR
220 PRINT "MULTI USER LUNAR LANDER - SERVER"
	PRINT "" 
  	INPUT "INITIAL ALTITUDE (2400):", TMP
	IF TMP=0 THEN H0=2400 ELSE H0=TMP END IF
 	INPUT "INITIAL SPEED (-30)", TMP
	IF TMP=0 THEN S0=-30 ELSE S0=TMP END IF
 	INPUT "INITIAL FUEL (500)", TMP
	IF TMP=0 THEN F0=500 ELSE F0=TMP END IF
 	INPUT "MAX LANDING SPEED (-15)", TMP
	IF TMP=0 THEN MAXSPD=-15 ELSE MAXSPD=TMP END IF
	PRINT ""
230 PRINT "WAITING FOR LOGINS"
240 GOSUB DOLOGIN

250 RESTART: RANDOMIZE \ PRINT ''
260 PRINT "GENERATING TERRAIN"
270 GOSUB GENTERR \ GOSUB GENLOCX
	ACT(0)=1 \ ACT(1)=1 ! CLIENTS CAN START DRAWING SCREENS
	WHILE (ACT(0)<>2 OR ACT(1)<>2) \ NEXT	! WAITING FOR CLIENTS
303 PRINT "RUNNING" 
305 GOSUB STRTGM

310 WHILE (1=1) ! MAINLOOP
	T(0)=T(0)+1 \ T(1)=T(1)+1	! INCREMENT TIME
312 	SLEEP 1

330 	IF H(0)>=0 THEN DUM=PLAUPD(0) END IF 
	IF ACT(0)=0 THEN ACT(0)=DETKIL(0) END IF
335 	IF H(1)>=0 THEN DUM=PLAUPD(1) END IF 
	IF ACT(1)=0 THEN ACT(1)=DETKIL(1) END IF

350	IF ACT(0)<>0 AND ACT(1)<>0 THEN
		PRINT "GAME OVER !!!"
		WHILE (ACT(0)<>10 OR ACT(1)<>10) \ NEXT ! WAIT FOR CLI ACK
		INPUT "ANOTHER GAME (Y/N) ?", YESNO
		YESNO=EDIT$(YESNO,32)
		IF YESNO="Y" THEN 
			GOTO RESTART
	 	ELSE
			ACT(0)=99 \ ACT(1)=99
			GOTO ENDTHIS
	 	END IF
	END IF
400 NEXT ! MAINLOOP
500 ENDTHIS: EXIT PROGRAM

2000 GENTERR: ! GENERATES TERRAIN PROFILE
	FOR I = STRTX TO 79
		TERRA(I-STRTX)=SCRHEI-INTEGER(RND*MTNHEI,BYTE)
	NEXT I
	LZSTART=INTEGER(RND*61, BYTE)
	LZEND=LZSTART+SIDSIZ-1
	FOR I=LZSTART TO LZEND
		TERRA(I)=124
	NEXT I
	LZSTART=LZSTART+STRTX \ LZEND=LZEND+STRTX
	RETURN

2100 DOLOGIN: ! WAITS FOR LOGINS FROM BOTH PLAYERS
	WHILE (CHKIN(0)<>1 OR CHKIN(1)<>1) \ NEXT
	RETURN

2200 GENLOCX: ! GENERATES INITIAL LOCATION OF EACH SHIP
	LOCX(0)=INTEGER(RND*67,BYTE)+STRTX
	GENL1: LOCX(1)=INTEGER(RND*67,BYTE)+STRTX
	IF ABS%(LOCX(0)-LOCX(1))<MINDIST THEN GOTO GENL1 END IF
	RETURN

2600 STRTGM: ! TELL PLAYERS GAME HAS STARTED
	T(0)=0 \ T(1)=0
	LOCY(0)=1 \ LOCY(1)=1
	H(0)=H0 \ H(1)=H(0)
	H0F=REAL(H0, SINGLE) \ H24=REAL(H0/24, SINGLE)
	S(0)=S0 \ S(1)=S(0)
	F(0)=F0 \ F(1)=F(0)
	P(0)=0 \ P(1)=0
	SIDE(0)=0 \ SIDE(1)=0
	COMM(0)=0 \ COMM(1)=0
	ACT(0)=0 \ ACT(1)=0
	OLDACL(0)=G \ OLDACL(1)=G
	CURACL(0)=G \ CURACL(1)=G
	TGTACL(0)=0 \ TGTACL(1)=0
 	BLOWN(0)=0 \ BLOWN(1)=0
	ROUNDS=ROUNDS+1
	RETURN

2700 DEF BYTE DETKIL (BYTE PLAYER) !  DETECT KILLS AND CRASHES
 ! COLLISION - RET 9
2705	IF ((ABS%(LOCX(0)-LOCX(1))<MINDIST) AND &
 	(ABS%(LOCY(0)-LOCY(1))<MINDIST)) THEN
		DETKIL=9
		GOTO ENDKIL
	END IF
2730 ! COLLISION WITH MOUNTAINS - DET 7
2735	IF (LOCY(PLAYER)+1)>=TERRA(LOCX(PLAYER)-STRTX) THEN
		DETKIL=7
		GOTO ENDKIL
	END IF
2740	IF H(PLAYER)<=0 THEN	
		IF (LOCX(PLAYER)-1)<LZSTART OR (LOCX(PLAYER)+1)>LZEND THEN
			DETKIL=8 ! OUTSIDE PAD
			GOTO ENDKIL
		ELSE
	                IF S(PLAYER) <= MAXSPD THEN
        	                DETKIL = 7 ! CRASHED
                	ELSE
                        	DETKIL = 5 ! ALIVE
                        	SCORE(PLAYER)=SCORE(PLAYER)+1
                	END IF
                	GOTO ENDKIL
		END IF
	END IF
 ! SHOT BY MISSILE - RET 6
2780	IF PLAYER=0 THEN OTHER=1 ELSE OTHER=0 END IF
2790	IF ABS%(MISL(OTHER,0)-LOCX(PLAYER))<2 AND &
	 ABS%(MISL(OTHER,1)-LOCY(PLAYER))<2 THEN
		DETKIL=6
		MISL(OTHER,0)=0
		F(PLAYER)=0
		SIDE(PLAYER)=0
		GOTO ENDKIL
	END IF
	IF ABS%(MISR(OTHER,0)-LOCX(PLAYER))<2 AND &
	ABS%(MISR(OTHER,1)-LOCY(PLAYER))<2 THEN
		DETKIL = 6 ! HIT
		MISR(OTHER,0)=0
		F(PLAYER)=0
		SIDE(PLAYER)=0
		GOTO ENDKIL
	END IF
2795	DETKIL = 0
2800 ENDKIL:
2805	END DEF

3000 DEF BYTE PLAUPD (BYTE PLAYER) ! PLAYER UPDATES
 ! DECODE COMMAND
	TMPCHR=CHR$(COMM(PLAYER))
	IF (TMPCHR>="0" AND TMPCHR<="9") THEN
		P(PLAYER)=COMM(PLAYER)-48
		GOTO CALCSTR
	END IF
	TMPCHR=EDIT$(TMPCHR,32)
	IF TMPCHR="C" AND BLOWN(PLAYER)=0 THEN
		SIDE(PLAYER)=SIDE(PLAYER)-SIDEG 
		COMM(PLAYER)=0
		GOTO CALCSTR
	END IF
	IF TMPCHR="V" AND BLOWN(PLAYER)=0 THEN
		SIDE(PLAYER)=SIDE(PLAYER)+SIDEG
		COMM(PLAYER)=0
		GOTO CALCSTR 
	END IF
	IF TMPCHR="D" AND MISL(PLAYER,0)=0 AND BLOWN(PLAYER)=0 THEN
		MISL(PLAYER,0)=LOCX(PLAYER)
		MISL(PLAYER,1)=LOCY(PLAYER)
		COMM(PLAYER)=0
		GOTO CALCSTR
	END IF
        IF TMPCHR="F" AND MISR(PLAYER,0)=0 AND BLOWN(PLAYER)=0 THEN
                MISR(PLAYER,0)=LOCX(PLAYER)
		MISR(PLAYER,1)=LOCY(PLAYER)
		COMM(PLAYER)=0
	END IF
 ! DO CALCS HERE
3120 CALCSTR:
	IF F(PLAYER) <= 0 THEN P(PLAYER)=0 END IF
	F(PLAYER) = F(PLAYER) - P(PLAYER)
	TGTACL(PLAYER)=P(PLAYER)-G
 ! CALC DELTA ACCEL NEEDED PROGRESSIVELY
	IF TGTACL(PLAYER)<CURACL(PLAYER) THEN
		CURACL(PLAYER)=CURACL(PLAYER)-1
	ELSE
		IF TGTACL(PLAYER)>CURACL(PLAYER) THEN
			CURACL(PLAYER)=CURACL(PLAYER)+1
		END IF
	END IF
 ! RSET T IF CHANGING ACCEL
	IF CURACL(PLAYER)<>OLDACL(PLAYER) THEN
		T(PLAYER)=1
		OLDACL(PLAYER)=CURACL(PLAYER)
	END IF
	S1=INTEGER(CURACL(PLAYER)/2*T(PLAYER),WORD)
	S(PLAYER)=S1+S0
	H(PLAYER)=H(PLAYER)+S(PLAYER)
	HF=REAL(H(PLAYER), SINGLE)
 ! CALC X
3160	LOCX(PLAYER)=LOCX(PLAYER)+SIDE(PLAYER)
	IF LOCX(PLAYER)>79 THEN LOCX(PLAYER)=12 END IF
	IF LOCX(PLAYER)<12 THEN LOCX(PLAYER)=79 END IF
 ! CALC Y
3175	HADJ=HF/H24-24
	LOCY(PLAYER)=ABS%(INTEGER(HADJ,WORD))
3180	IF LOCY(PLAYER)<3 THEN LOCY(PLAYER)=3 END IF
3185	IF LOCY(PLAYER)>23 THEN LOCY(PLAYER)=23 END IF
 ! CALC MISSILES
3190	IF MISL(PLAYER,0)>13 AND MISL(PLAYER,1)<24 THEN
		MISL(PLAYER,0)=MISL(PLAYER,0)-2
		MISL(PLAYER,1)=MISL(PLAYER,1)+1
	ELSE
		MISL(PLAYER,0)=0
	END IF
	IF MISR(PLAYER,0)>0 AND MISR(PLAYER,0)<78 AND MISR(PLAYER,1)<24 &
	THEN
                MISR(PLAYER,0)=MISR(PLAYER,0)+2
		MISR(PLAYER,1)=MISR(PLAYER,1)+1
	ELSE
		MISR(PLAYER,0)=0      
	END IF
3200	PLAUPD = 0
	END DEF

7000 %INCLUDE "COMCODE"




